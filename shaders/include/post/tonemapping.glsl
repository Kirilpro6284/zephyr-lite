/*
    https://github.com/dmnsgn/glsl-tone-map

    Copyright (C) 2019 Damien Seguin

    Permission is hereby granted, free of charge, to any person obtaining 
    a copy of this software and associated documentation files (the 
    "Software"), to deal in the Software without restriction, including 
    without limitation the rights to use, copy, modify, merge, publish, 
    distribute, sublicense, and/or sell copies of the Software, and to 
    permit persons to whom the Software is furnished to do so, subject to 
    the following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, 
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF 
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND 
    NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE 
    LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION 
    OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION 
    WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
*/

#ifndef INCLUDE_TONEMAPPING
    #define INCLUDE_TONEMAPPING

    // Missing Deadlines (Benjamin Wrensch): https://iolite-engine.com/blog_posts/minimal_agx_implementation
    // Filament: https://github.com/google/filament/blob/main/filament/src/ToneMapper.cpp#L263
    // https://github.com/EaryChow/AgX_LUT_Gen/blob/main/AgXBaseRec2020.py

    // Three.js: https://github.com/mrdoob/three.js/blob/4993e3af579a27cec950401b523b6e796eab93ec/src/renderers/shaders/ShaderChunk/tonemapping_pars_fragment.glsl.js#L79-L89
    // Matrices for rec 2020 <> rec 709 color space conversion
    // matrix provided in row-major order so it has been transposed
    // https://www.itu.int/pub/R-REP-BT.2407-2017
    const mat3 LINEAR_REC2020_TO_LINEAR_SRGB = mat3(
        1.6605, -0.1246, -0.0182,
        -0.5876, 1.1329, -0.1006,
        -0.0728, -0.0083, 1.1187
    );

    const mat3 LINEAR_SRGB_TO_LINEAR_REC2020 = mat3(
        0.6274, 0.0691, 0.0164,
        0.3293, 0.9195, 0.0880,
        0.0433, 0.0113, 0.8956
    );

    // Converted to column major from blender: https://github.com/blender/blender/blob/fc08f7491e7eba994d86b610e5ec757f9c62ac81/release/datafiles/colormanagement/config.ocio#L358
    const mat3 AgXInsetMatrix = mat3(
        0.856627153315983, 0.137318972929847, 0.11189821299995,
        0.0951212405381588, 0.761241990602591, 0.0767994186031903,
        0.0482516061458583, 0.101439036467562, 0.811302368396859
    );

    // Converted to column major and inverted from https://github.com/EaryChow/AgX_LUT_Gen/blob/ab7415eca3cbeb14fd55deb1de6d7b2d699a1bb9/AgXBaseRec2020.py#L25
    // https://github.com/google/filament/blob/bac8e58ee7009db4d348875d274daf4dd78a3bd1/filament/src/ToneMapper.cpp#L273-L278
    const mat3 AgXOutsetMatrix = mat3(
        1.1271005818144368, -0.1413297634984383, -0.14132976349843826,
        -0.11060664309660323, 1.157823702216272, -0.11060664309660294,
        -0.016493938717834573, -0.016493938717834257, 1.2519364065950405
    );

    const float AgxMinEv = -12.47393;
    const float AgxMaxEv = 4.026069;

    // Sample usage
    vec3 agxCdl(vec3 color, vec3 slope, vec3 offset, vec3 power, float saturation) {
        color = LINEAR_SRGB_TO_LINEAR_REC2020 * color; // From three.js

        // 1. agx()
        // Input transform (inset)
        color = AgXInsetMatrix * color;

        color = max(color, 1e-10); // From Filament: avoid 0 or negative numbers for log2

        // Log2 space encoding
        color = clamp(log2(color), AgxMinEv, AgxMaxEv);
        color = (color - AgxMinEv) / (AgxMaxEv - AgxMinEv);

        color = clamp(color, 0.0, 1.0); // From Filament

        // Apply sigmoid function approximation
        // Mean error^2: 3.6705141e-06
        vec3 x2 = color * color;
        vec3 x4 = x2 * x2;
        color = + 15.5     * x4 * x2
                - 40.14    * x4 * color
                + 31.96    * x4
                - 6.868    * x2 * color
                + 0.4298   * x2
                + 0.1191   * color
                - 0.00232;

        // 2. agxLook()
        color = pow(color * slope + offset, power);
        const vec3 lw = vec3(0.2126, 0.7152, 0.0722);
        float luma = dot(color, lw);
        color = luma + saturation * (color - luma);

        // 3. agxEotf()
        // Inverse input transform (outset)
        color = AgXOutsetMatrix * color;

        // sRGB IEC 61966-2-1 2.2 Exponent Reference EOTF Display
        // NOTE: We're linearizing the output here. Comment/adjust when
        // *not* using a sRGB render target
        color = pow(max(vec3(0.0), color), vec3(2.2)); // From filament: max()

        color = LINEAR_REC2020_TO_LINEAR_SRGB * color; // From three.js
        // Gamut mapping. Simple clamp for now.
            color = clamp(color, 0.0, 1.0);

        return color;
    }

    vec3 agx(vec3 color) {
        return agxCdl(color, vec3(1.0), vec3(0.0), vec3(1.0), 1.0);
    }

    // Lottes 2016, "Advanced Techniques and Optimization of HDR Color Pipelines"
    vec3 lottes(vec3 x) {
        const vec3 a = vec3(1.6);
        const vec3 d = vec3(0.977);
        const vec3 hdrMax = vec3(8.0);
        const vec3 midIn = vec3(0.18);
        const vec3 midOut = vec3(0.267);

        const vec3 b =
            (-pow(midIn, a) + pow(hdrMax, a) * midOut) /
            ((pow(hdrMax, a * d) - pow(midIn, a * d)) * midOut);
        const vec3 c =
            (pow(hdrMax, a * d) * pow(midIn, a) - pow(hdrMax, a) * pow(midIn, a * d) * midOut) /
            ((pow(hdrMax, a * d) - pow(midIn, a * d)) * midOut);

        return pow(x, a) / (pow(x, a * d) * b + c);
    }

    // Khronos PBR Neutral Tone Mapper
    // https://github.com/KhronosGroup/ToneMapping/tree/main/PBR_Neutral

    // Input color is non-negative and resides in the Linear Rec. 709 color space.
    // Output color is also Linear Rec. 709, but in the [0, 1] range.
    vec3 neutral(vec3 color) {
        const float startCompression = 0.8 - 0.04;
        const float desaturation = 0.15;

        float x = min(color.r, min(color.g, color.b));
        float offset = x < 0.08 ? x - 6.25 * x * x : 0.04;
        color -= offset;

        float peak = max(color.r, max(color.g, color.b));
        if (peak < startCompression) return color;

        const float d = 1.0 - startCompression;
        float newPeak = 1.0 - d * d / (peak + d - startCompression);
        color *= newPeak / peak;

        float g = 1.0 - 1.0 / (desaturation * (peak - newPeak) + 1.0);
        return mix(color, vec3(newPeak), g);
    }

    vec3 reinhard2(vec3 x) {
        const float L_white = 4.0;

        return (x * (1.0 + x / (L_white * L_white))) / (1.0 + x);
    }

    vec3 exponential (vec3 color, float exposure) {
        return 1.0 - exp(-exposure * color);
    }

    // This is done this way because for some reason hardware interpolation causes color banding
    vec3 tonyMcMapface (vec3 color) {
        vec3 coord = 47.0 * color / (color + 1.0);
        ivec3 texel = ivec3(coord);

        vec3 result = vec3(0.0);

        for (int i = 0; i < 8; i++) {
            ivec3 offset = ivec3(i >> 2, i >> 1, i) & ivec3(1);

            result += trilinearWeight(coord, vec3(offset)) * texelFetch(depthtex2, texel + offset, 0).rgb;
        }

        return result;
    }

    #if TONEMAP_OPERATOR == 0
        #define tonemap(color, exposure) pow(agx(color * exposure), vec3(1.0 / 2.2))
    #elif TONEMAP_OPERATOR == 1
        #define tonemap(color, exposure) pow(lottes(color * exposure), vec3(1.0 / 2.2))
    #elif TONEMAP_OPERATOR == 2
        #define tonemap(color, exposure) pow(neutral(color * exposure), vec3(1.0 / 2.2))
    #elif TONEMAP_OPERATOR == 3
        #define tonemap(color, exposure) pow(reinhard2(color * exposure), vec3(1.0 / 2.2))
    #elif TONEMAP_OPERATOR == 4
        #define tonemap(color, exposure) pow(exponential(color, exposure), vec3(1.0 / 2.2))
    #elif TONEMAP_OPERATOR == 5
        #define tonemap(color, exposure) pow(tonyMcMapface(color * exposure), vec3(1.0 / 2.2))
    #endif

#endif